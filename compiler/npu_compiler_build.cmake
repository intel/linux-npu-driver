# Copyright (C) 2022-2025 Intel Corporation
#
# SPDX-License-Identifier: MIT

include(compiler_source.cmake)

include(ProcessorCount)
ProcessorCount(PARALLEL_PROCESSES)

set(NPU_COMPILER_BINARY_DIR "${CMAKE_CURRENT_BINARY_DIR}/build_npu_compiler")
file(MAKE_DIRECTORY ${NPU_COMPILER_BINARY_DIR})

set(THREADING "TBB" CACHE STRING "Build NPU Compiler with specific THREADING option")

set(NPU_COMPILER_INSTALL_PREFIX ${CMAKE_CURRENT_BINARY_DIR}/npu_compiler)
set(NPU_COMPILER_PACKAGE_DIR ${NPU_COMPILER_INSTALL_PREFIX}/cid)

include(ExternalProject)

list(APPEND NPU_COMPILER_CMAKE_ARGS "-D CMAKE_BUILD_TYPE=Release")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D CMAKE_MAKE_PROGRAM=${CMAKE_MAKE_PROGRAM}")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D BUILD_COMPILER_FOR_DRIVER=ON")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D BUILD_SHARED_LIBS=OFF")
# CLANG_FORMAT and NCC_STYLE is set to OFF to avoid LLVMDemangle doubled target issue
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_CLANG_FORMAT=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_CLANG_TIDY=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_NCC_STYLE=OFF")
# Copied from "how_to_build_driver_compiler" document
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_AUTO=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_AUTO_BATCH=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_BLOB_DUMP=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_FUNCTIONAL_TESTS=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_HETERO=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_INTEL_CPU=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_INTEL_GPU=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_JS=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_MULTI=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_INTEL_NPU=ON")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_INTEL_NPU_INTERNAL=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_INTEL_NPU_PROTOPIPE=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_OV_IR_FRONTEND=ON")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_OV_JAX_FRONTEND=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_OV_ONNX_FRONTEND=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_OV_PADDLE_FRONTEND=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_OV_PYTORCH_FRONTEND=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_OV_TF_FRONTEND=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_OV_TF_LITE_FRONTEND=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_PROXY=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_SAMPLES=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_TBBBIND_2_5=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_TEMPLATE=OFF")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ENABLE_TESTS=OFF")
# WA in case libgflags is installed in system
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D gflags_DIR=${CMAKE_CURRENT_SOURCE_DIR}/openvino_modules")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D OPENVINO_EXTRA_MODULES=${NPU_COMPILER_SOURCE_DIR}")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D OUTPUT_ROOT=${NPU_COMPILER_BINARY_DIR}")
list(APPEND NPU_COMPILER_CMAKE_ARGS "-D THREADING=${THREADING}")

if (ANDROID)
  # First build native tools required for NPU compiler
  set(NPU_COMPILER_NATIVE_TOOLS_BUILD npu_compiler_native_tools_build)
  set(NPU_COMPILER_NATIVE_TOOLS_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/build_npu_compiler_native_tools)

  ExternalProject_Add(
    ${NPU_COMPILER_NATIVE_TOOLS_BUILD}
    DOWNLOAD_COMMAND ""
    DEPENDS npu_compiler_source ${NPU_COMPILER_BUILD_DEPENDS}
    SOURCE_DIR ${NPU_COMPILER_OPENVINO_SOURCE_DIR}
    BINARY_DIR ${NPU_COMPILER_NATIVE_TOOLS_BINARY_DIR}
    CMAKE_ARGS
      ${NPU_COMPILER_CMAKE_ARGS}
    BUILD_COMMAND
      ${CMAKE_COMMAND}
        --build ${NPU_COMPILER_NATIVE_TOOLS_BINARY_DIR}
        --target npureg-tblgen mlir-headers mlir-generic-headers mlir-linalg-ods-yaml-gen flatc
        --parallel ${PARALLEL_PROCESSES}
    INSTALL_COMMAND
      mkdir -p ${NPU_COMPILER_BINARY_DIR}/build-modules/npu_compiler/thirdparty/llvm-project/llvm/NATIVE &&
      cp -r ${NPU_COMPILER_NATIVE_TOOLS_BINARY_DIR}/build-modules/npu_compiler/thirdparty/llvm-project/llvm/bin ${NPU_COMPILER_BINARY_DIR}/build-modules/npu_compiler/thirdparty/llvm-project/llvm/NATIVE/
    BYPRODUCTS
      ${NPU_COMPILER_BINARY_DIR}/bin/intel64/Release/flatc
      ${NPU_COMPILER_BINARY_DIR}/bin/intel64/Release/npureg-tblgen
  )

  # Android specific settings
  list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ANDROID_ABI=${ANDROID_ABI}")
  list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ANDROID_PLATFORM=${ANDROID_PLATFORM}")
  list(APPEND NPU_COMPILER_CMAKE_ARGS "-D ANDROID_STL=${ANDROID_STL}")
  list(APPEND NPU_COMPILER_CMAKE_ARGS "-D CMAKE_CXX_FLAGS_INIT='-frtti'")
endif()

list(APPEND NPU_COMPILER_CMAKE_ARGS "-D CMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}")

ExternalProject_Add(
  npu_compiler_build
  DOWNLOAD_COMMAND ""
  DEPENDS npu_compiler_source ${NPU_COMPILER_BUILD_DEPENDS} ${NPU_COMPILER_NATIVE_TOOLS_BUILD}
  SOURCE_DIR ${NPU_COMPILER_OPENVINO_SOURCE_DIR}
  BINARY_DIR ${NPU_COMPILER_BINARY_DIR}
  CMAKE_ARGS
    ${NPU_COMPILER_CMAKE_ARGS}
  BUILD_COMMAND
    ${CMAKE_COMMAND}
      --build ${NPU_COMPILER_BINARY_DIR}
      --target compilerTest profilingTest vpuxCompilerL0Test loaderTest
      --parallel ${PARALLEL_PROCESSES}
  INSTALL_COMMAND
    ${CMAKE_COMMAND}
      --install ${NPU_COMPILER_BINARY_DIR}
      --prefix ${NPU_COMPILER_INSTALL_PREFIX}
      --component CiD
  BYPRODUCTS
    ${NPU_COMPILER_PACKAGE_DIR}/lib/libnpu_driver_compiler.so
    ${NPU_COMPILER_PACKAGE_DIR}/vpux_elf/lib/Release/libnpu_elf.a
)

add_dependencies(npu_compiler npu_compiler_build)

# Extra command to prepare a standalone package with NPU compiler
string(REPLACE "/" "_" NPU_COMPILER_TAG_PACKAGE ${NPU_COMPILER_TAG})
set(NPU_COMPILER_PACKAGE_NAME "npu-drv-compiler-${TARGET_DISTRO}-${NPU_COMPILER_TAG_PACKAGE}-${BUILD_NUMBER}")

add_custom_target(npu_compiler_package
  COMMAND
    tar -C ${NPU_COMPILER_INSTALL_PREFIX} -czf ${CMAKE_BINARY_DIR}/${NPU_COMPILER_PACKAGE_NAME}.tar.gz --transform='s,/cid,/npu_compiler,' ./cid/
  DEPENDS npu_compiler_build
  BYPRODUCTS ${CMAKE_BINARY_DIR}/${NPU_COMPILER_PACKAGE_NAME}.tar.gz)

install(
  FILES ${CMAKE_BINARY_DIR}/${NPU_COMPILER_PACKAGE_NAME}.tar.gz
  DESTINATION .
  COMPONENT npu_compiler_package
  EXCLUDE_FROM_ALL)
